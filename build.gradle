subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

project(':qbit') {

    apply plugin: 'signing'

    group = 'io.advantageous.qbit'
    version = '1.0.0-SNAPSHOT'

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.10'
        testCompile project(':qbit-boon')
        compile "org.slf4j:slf4j-api:[1.7,1.8)"
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

    signing {
        required false
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    try {
                        authentication(userName: sonatypeUsername, password: sonatypePassword)
                    } catch (MissingPropertyException ignore) {
                    }
                }

                pom.project {
                    name 'qbit'
                    packaging 'jar'
                    description 'Go Channels inspired Java lib'
                    url 'https://github.com/RichardHightower/qbit'

                    scm {
                        url 'scm:git@github.com:RichardHightower/qbit.git'
                        connection 'scm:git@github.com:RichardHightower/qbit.git'
                        developerConnection 'scm:git@github.com:RichardHightower/qbit.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'richardHightower'
                            name 'Richard Hightower'
                        }
                        developer {
                            id 'sailorgeoffrey'
                            name 'Geoffrey Chandler'
                        }
                    }
                }
            }
        }
    }
}

project(':qbit-boon') {
    sourceSets.main.resources.srcDir 'src/main/java'
    dependencies {
        compile project(':qbit')
        compile group: 'io.fastjson', name: 'boon', version: '0.28'
    }
}

project(':examples') {
    sourceSets.main.resources.srcDir 'src/main/java'
    dependencies {
        compile project(':qbit-boon')
    }
}
